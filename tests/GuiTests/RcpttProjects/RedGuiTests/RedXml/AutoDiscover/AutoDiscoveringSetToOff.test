--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _eXb6MPp7EeW7_PzjO98BIw
Element-Name: AutoDiscoveringSetToOff
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _-gXO8P_gEeWRL4nRKA71zA
Runtime-Version: 2.3.0.201806262310
Save-Time: 2/15/19 4:19 PM
Testcase-Type: ecl
Verifications: _gGHWMP_kEeWRL4nRKA71zA

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

// check default settings
get-preferences-menu | click
get-window Preferences | get-tree | select "Robot Framework/Libraries"
with [get-window Preferences | get-group "Libraries autodiscovering"] {
    get-property childCount | equals 3 | verify-true
    get-property caption | equals "Libraries autodiscovering" | verify-true
    get-property "getChildren().Control[0].getText()" | equals "Auto discover libraries after test suite save action" 
        | verify-true
    get-property "getChildren().Control[0].getSelection()" | equals true | verify-true
    get-property "getChildren().Control[1].getText()" | equals "Show discovering summary after test suite save action" 
        | verify-true
    get-property "getChildren().Control[1].getSelection()" | equals false | verify-true
    get-property "getChildren().Control[2].getText()" 
        | equals "Add project modules recursively to PYTHONPATH/CLASSPATH during autodiscovering on virtualenv" 
        | verify-true
    get-property "getChildren().Control[2].getSelection()" | equals false | verify-true
}

// uncheck first option - second is disbaled and unchecked
with [get-window Preferences] {
    get-group "Libraries autodiscovering" | get-button "Auto discover libraries after test suite save action" | uncheck
}
with [get-window Preferences | get-group "Libraries autodiscovering"] {
    with [get-button "Auto discover libraries after test suite save action"] {
        get-property enablement | equals true | verify-true
        get-property selected | equals false | verify-true
    }
    with [get-button "Show discovering summary after test suite save action"] {
        get-property enablement | equals false | verify-true
        get-property selected | equals false | verify-true
    }
}
get-window Preferences | get-button "Apply and Close" | click


//add text to Suite
get-view "Project Explorer" | get-tree | select "RobotProject/lib_with_variable.robot" | double-click
with [get-editor "lib_with_variable.robot" | get-text-viewer] {
    set-caret-pos 8 12
    type-text 5
    key-type "M1+s"
}

//check that no window is shown
verify-error {get-window "Discovering libraries summary"}

//check "Auto discover libs.."
get-preferences-menu | click
with [get-window Preferences] {
    with [get-group "Libraries autodiscovering" | get-button "Auto discover libraries after test suite save action"] {
        check
    }
    get-button "Apply and Close" | click
}
get-tab-folder -index 1 | get-tab-item "lib_with_variable.robot" | click
with [get-editor "lib_with_variable.robot" | get-text-viewer] {
    type-text 3
    key-type "M1+s"
}
wait 3000

//check that no window is shown
verify-error {get-window "Discovering libraries summary"}

//check that libs were added to red.xml
with [get-tab-folder -index 1] {
    get-property "pages[0]" | equals "lib_with_variable.robot" | verify-true
    get-property activePage | equals "lib_with_variable.robot" | verify-true
    get-property tabCount | equals 1 | verify-true
}

get-view "Project Explorer" | get-tree | select "RobotProject/red.xml" | select "RobotProject/red.xml" | double-click
get-editor "RobotProject/red.xml" | get-tab-folder | get-tab-item "Referenced libraries" | click
with [get-editor "RobotProject/red.xml" | get-section "Referenced libraries" | get-table] {
    get-property itemCount | equals 1 | verify-true
    get-property "getItems().TableItem[0].getText()" | equals "multiply - RobotProject" | verify-true
}

//remove lib from red.xml and close red.xml
with [get-editor "RobotProject/red.xml"] {
    with [get-section "Referenced libraries" | get-table] {
        select "multiply - RobotProject" | key-type Del
        key-type "M1+s"
    }
    wait 500
    close
}

//add value to Suite
with [get-editor "lib_with_variable.robot" | get-text-viewer] {
    set-caret-pos 8 14
    type-text 5
    key-type "M1+s"
}


//check if libs were add to red.xml
get-view "Project Explorer" | get-tree | select "RobotProject/red.xml" | double-click
with [get-editor "RobotProject/red.xml" | get-section "Referenced libraries" | get-table] {
    get-property itemCount | equals 1 | verify-true
    get-property "getItems().TableItem[0].getText()" | equals "multiply - RobotProject" | verify-true
}
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
