--- RCPTT testcase ---
Format-Version: 1.0
Element-Name: CreateResourceFile
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _bsSM8McMEei8MYxPdH4l7Q
Runtime-Version: 2.2.0.201706152316
Save-Time: 10/3/18 6:51 PM
Testcase-Type: ecl
Verifications: _ipfpIMcsEei8MYxPdH4l7Q

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

//add new resource file file
get-view "Project Explorer" | get-tree | select RobotProject | get-menu -path "New/Robot Resource file" | click
with [get-window "New Robot Resource file"] {
    get-editbox -after [get-label "File name:"] | set-text res
    get-button "as .resource file" | click
    get-button Finish | click
}

//check if it has all sections
get-editor "res.resource" | get-text-viewer | key-type "M1+SPACE"
with [get-window -from "CompletionProposalPopup.createProposalSelector()" | get-table] {
    get-property itemCount | equals 5 | verify-true
    get-property "getItems().TableItem[0].getText()" | equals "*** Keywords ***" | verify-true
    get-property "getItems().TableItem[1].getText()" | equals "*** Settings ***" | verify-true
    get-property "getItems().TableItem[2].getText()" | equals "*** Tasks ***" | verify-true
    get-property "getItems().TableItem[3].getText()" | equals "*** Test Cases ***" | verify-true
    get-property "getItems().TableItem[4].getText()" | equals "*** Variables ***" | verify-true
}

//add KW section
with [get-window -from "CompletionProposalPopup.createProposalSelector()" | get-table] {
    select "\\*\\*\\* Keywords \\*\\*\\*" | key-type Enter
    click -default
}
//add simple kw
with [get-editor "res.resource" | get-text-viewer] {
    set-caret-pos 1 17
    key-type Enter
    type-text "kw 1"
    key-type Enter
    type-text "log  5"
    key-type "M1+s"
}

with [get-editor "res.resource" | get-tab-folder] {
    get-property "pages[0]" | equals Keywords | verify-true
    get-property "pages[1]" | equals Settings | verify-true
    get-property "pages[2]" | equals Variables | verify-true
    get-property "pages[3]" | equals Source | verify-true
    get-property activePage | equals Source | verify-true
    get-property tabCount | equals 4 | verify-true
}

//open test
get-view "Project Explorer" | get-tree | select "RobotProject/TestFile.robot" | select "RobotProject/TestFile.robot" 
    | double-click
with [get-editor "TestFile.robot" | get-text-viewer] {
    set-caret-pos 1 1
    //add settings
    key-type Enter
    key-type Up
    key-type "M1+SPACE"
    key-type Down
    key-type Enter
    
    //add resource
    type-text RES
    key-type "M1+SPACE"
    key-type Enter
    wait 300
    key-type Enter
    key-type Down
    key-type Enter
    
    //add test
    type-text "test 1"
    key-type Enter
    type-text "kw 1"
    key-type "M1+s"
}

get-editor "TestFile.robot" | get-text-viewer | get-property text | equals "*** Settings ***\n"
    + "Resource    res.resource\n"
    + "*** Test Cases ***\n"
    + "test 1\n"
    + "    kw 1" | verify-true

//run test - now is not supported
get-view "Project Explorer" | get-tree | select "RobotProject/TestFile.robot" | get-menu -path "Run As/1 Robot Test" 
    | click
    
wait-for-message-log  -last_line  "RobotProject.TestFile.test 1" 
get-view "Message Log" | get-editbox | get-property text | equals "Starting test: RobotProject.TestFile.test 1\n"
    + ".* : FAIL : No keyword with name 'kw 1' found.\n"
    + "Ending test: RobotProject.TestFile.test 1" | verify-true
    
//add TC section - now window should be shown     
with [get-editor "res.resource"] {
    click
    with [get-text-viewer] {
        set-caret-pos 4 1
        key-type "M1+SPACE"
        key-type Down -times 3
        key-type Enter
        key-type "M1+s"
    }
}
get-view Problems | click
get-view Problems | get-tree | get-item 
    -path "Errors \\(1 item\\)/'Test Cases' table is not supported inside resource file" | get-property enablement 
    | equals false | verify-true 
       
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
